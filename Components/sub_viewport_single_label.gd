extends SubViewport
class_name SubViewPortSingleLabel
# Used as a way to convert text into a texture
@onready var label: Label = $PanelContainer/Label
@export var text = "":
	set(value):
		set_text(value)
const SUB_VIEWPORT_SINGLE_LABEL = preload("uid://cnrjg1q6m5y36")


func _ready() -> void:
	# Themes do not cross SubViewport, must force theme following
	if not get_tree().root.is_node_ready():
		await get_tree().root.ready
	resize(Vector2(16, 16))
	_copy_root_theme()
	get_tree().root.theme_changed.connect(_copy_root_theme)
	
	
func _copy_root_theme():
	var theme_resource: Theme = get_tree().root.get_theme()
	if theme_resource:
		if not theme_resource.is_type_variation("EmojiFont", "Label"):
			theme_resource.set_type_variation("EmojiFont", "Label")
		$PanelContainer/Label.set_theme(theme_resource)
		var label_set = LabelSettings.new()
		var theme_font = theme_resource.get_font("font", "EmojiFont")
		var theme_color = theme_resource.get_color("font_color", "EmojiFont")
		var theme_size = theme_resource.get_font_size("font_size", "EmojiFont")
		label_set.font = theme_font if theme_font else $PanelContainer/Label.label_settings.font
		label_set.font_size = $PanelContainer/Label.label_settings.font_size if $PanelContainer/Label.label_settings.font_size else theme_size
		label_set.font_color = theme_color if theme_resource.has_color("font_color", "EmojiFont") else $PanelContainer/Label.label_settings.font_color
		size = Vector2(label_set.font_size, label_set.font_size)
		$PanelContainer/Label.label_settings = label_set

# Converts an emoji into a texture, just call get_texture() if already have object
static func texture_from_text(emoji_text:String, caller: Object) -> ViewportTexture:
	# Attempt to reuse past subviewports, else make a new one
	var subview = get_make(emoji_text, caller)
	return subview.get_texture()

static func get_make(emoji_text:String, caller: Object) -> SubViewPortSingleLabel:
	var subview = caller.get_node_or_null(emoji_text)
	if not subview or not subview is SubViewPortSingleLabel:
		subview  = SUB_VIEWPORT_SINGLE_LABEL.instantiate()
		subview.set_text(emoji_text)
		subview.name = emoji_text
		caller.add_child(subview)
	return subview

func set_text(new_text: String):
	$PanelContainer/Label.text = new_text

func resize(vector: Vector2):
	# Do nothing on 0 or null call
	if not vector:
		return
	# Do nothing on no-change call
	if vector.y == size.y:
		return
	
	size = vector
	var label_set = LabelSettings.new()
	label_set.font_size = vector.y
	label_set.font = $PanelContainer/Label.label_settings.font
	label_set.font_color = $PanelContainer/Label.label_settings.font_color
	$PanelContainer/Label.label_settings = label_set
	
		
